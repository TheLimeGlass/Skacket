import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'eclipse'
	id 'java'
}

def latestJava = 17
def oldestJava = 8

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(latestJava))
}

tasks.withType(JavaCompile).configureEach {
	options.compilerArgs += ['-source', '' + oldestJava, '-target', '' + oldestJava]
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

repositories {
	mavenCentral()

	// Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	// Anvil GUI
	maven {
		url "https://repo.codemc.io/repository/maven-snapshots/"
	}

	// Paper and Adventure API
	maven {
		url 'https://repo.papermc.io/repository/maven-public/'
	}

	// ProtocolLib
	maven {
		url "https://repo.dmulloy2.net/repository/public/"
	}

	// Skript
	maven {
		url 'https://repo.skriptlang.org/releases'
	}

}

dependencies {

	// Nullable annotation
	implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.2.700')

	// Spigot/Bukkit
	implementation (group: 'io.papermc.paper', name: 'paper-api', version: '1.20.2-R0.1-SNAPSHOT')

	// Skript
	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.7.0') {
		transitive = false
	}

	// ProtocolLib
	implementation (group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.1.0") {
		transitive = false
	}

	// Adventure API serializer to text components for Bukkit.
	shadow group: 'net.kyori', name: 'adventure-text-serializer-bungeecord', version: '4.3.2'

	// AnvilGUI
	shadow (group: 'net.wesjd', name: 'anvilgui', version: '1.9.0-SNAPSHOT')

	// bStats
	shadow group: 'org.bstats', name: 'bstats-bukkit', version: '3.0.2'

	shadow fileTree(dir: 'libraries', include: '*.jar')
	shadow project(':GlowingAPI')

}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
	from ('lang') {
        include '**/*'
        into 'lang/'
    }
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveVersion = project.property("version")
	archiveFileName = 'Skacket-' + project.property("version") + '.jar'
	relocate 'eu.endercentral.crazy_advancements', 'me.limeglass.skacket.crazyadvancements'
	relocate 'org.bstats', 'me.limeglass.skacket.bstats'
	dependencies {
		exclude(dependency('org.spigotmc:'))
		exclude(dependency('org.yaml:'))
		exclude(dependency('io.netty:'))
		include(dependency('org.bstats:bstats-bukkit'))
		include(dependency('org.bstats:bstats-base'))
		include(project(':GlowingAPI'))
		include(dependency('net.wesjd:anvilgui'))
		include(dependency('net.kyori:adventure-text-serializer-bungeecord'))
	}
}
