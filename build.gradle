import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'maven-publish'
	id 'eclipse'
	id 'java'
}

repositories {
	mavenCentral()
	jcenter()

	//Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	// ProtocolLib
	maven {
		url "https://repo.dmulloy2.net/repository/public/"
	}

	maven {
		url 'https://maven.pkg.github.com/Sitrica/GlowingAPI/'
		credentials {
			username = "Sitrica"
			password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_PACKAGES_KEY")
		}
	}

	// Anvil GUI
	maven {
		url "https://repo.codemc.io/repository/maven-snapshots/"
	}

	//Jitpack for Skript
	maven {
		url 'https://jitpack.io'
	}

}

dependencies {

	//Nullable annotation
	implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.2.600')

	//Spigot/Bukkit
	implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.16.4-R0.1-SNAPSHOT')

	//Skript
	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.4.1') {
		transitive = false
	}

	// ProtocolLib
	implementation (group: "com.comphenix.protocol", name: "ProtocolLib", version: "4.6.1-SNAPSHOT");

	shadow (group: 'com.sitrica', name: 'GlowingAPI', version: '1.0.0')

	// AnvilGUI
	shadow (group: 'net.wesjd', name: 'anvilgui', version: '1.5.0-SNAPSHOT')

	shadow (group: 'org.json', name: 'json', version: '20201115')

}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
	from ('lang') {
        include '**/*'
        into 'lang/'
    }
	from (sourceSets.main.resources.srcDirs) {
		include '**/*.yml'
	}
}

shadowJar {
	configurations = [project.configurations.shadow]
	archiveVersion = version
	baseName = project.name
	classifier = ''
	relocate 'net.wesjd.anvilgui', 'me.limeglass.skacket.anvilgui'
	minimize()
	dependencies {
		exclude(dependency('org.spigotmc:'))
		exclude(dependency('org.yaml:'))
		exclude(dependency('io.netty:'))
	}
}
